defmodule DAG.Parser.Adapters.ElixirTest do
  use Gust.DataCase, async: false
  import Gust.FSHelpers
  alias Gust.DAG.Parser.Adapters.Elixir, as: Adapter

  setup do
    dir = make_rand_dir!("dags")

    on_exit(fn ->
      File.rm_rf!(dir)
    end)

    {:ok, tmp_dir: dir}
  end

  describe "extension/0" do
    test "returns .ex" do
      assert ".ex" == Adapter.extension()
    end
  end

  describe "parse_file/1" do
    test "file is not valid", %{tmp_dir: dags_folder} do
      dag_definition = """
        defmodule MyValidDag do
          use Gust.DSL, schedule: "* * * * *"
          |>
        end
      """

      dag_name = "my_name"
      file = "#{dags_folder}/#{dag_name}.ex"
      File.write!(file, dag_definition)

      assert {:error,
              {
                [{:line, 4}, {:column, 3}],
                "syntax error before: ",
                "'end'"
              }} = Adapter.parse_file(file)
    end

    test "file is not including DSL not valid dag", %{tmp_dir: dags_folder} do
      dag_definition = """
        defmodule MyValidDag do
        end
      """

      dag_name = "my_name"
      file = "#{dags_folder}/#{dag_name}.ex"
      File.write!(file, dag_definition)

      assert {:error, {[], "use Gust.DSL not found", ""}} = Adapter.parse_file(file)
    end

    test "file is valid dag", %{tmp_dir: dags_folder} do
      dag_definition = """
        defmodule MyValidDagEmpty do
          use Gust.DSL, schedule: "* * * * *"

          def dup, do: :ok
          def dup, do: :error

          task :bye do
            # saying bye
          end

          task :hi, downstream: [:bye], store_result: true do
            # saying hi
          end
        end
      """

      dag_name = "my_name"
      file = "#{dags_folder}/#{dag_name}.ex"
      File.write!(file, dag_definition)

      warning_message =
        "this clause for dup/0 cannot match because a previous clause at line 4 always matches"

      warnings = [
        %{
          message: warning_message,
          position: {5, 9},
          file: file,
          stacktrace: [],
          source: file,
          span: nil,
          severity: :warning
        }
      ]

      dag_def = %Gust.DAG.Definition{
        name: dag_name,
        mod: MyValidDagEmpty,
        task_list: ["hi", "bye"],
        stages: [["hi"], ["bye"]],
        options: [{:schedule, "* * * * *"}],
        messages: warnings,
        file_path: file,
        error: %{},
        tasks: %{
          "bye" => %{
            upstream: MapSet.new(["hi"]),
            downstream: MapSet.new([]),
            store_result: false,
            skip_if: nil,
            map_over: nil
          },
          "hi" => %{
            upstream: MapSet.new([]),
            downstream: MapSet.new(["bye"]),
            store_result: true,
            skip_if: nil,
            map_over: nil
          }
        }
      }

      assert {:ok, ^dag_def} = Adapter.parse_file(file)
    end

    test "file has code errors", %{tmp_dir: dags_folder} do
      dag_definition = """
        defmodule CompiledErroredDag do
          use Gust.DSL

          kaboomm
        end
      """

      dag_name = "my_name"
      file = "#{dags_folder}/#{dag_name}.ex"
      File.write!(file, dag_definition)

      {:ok, dag_def} = Adapter.parse_file(file)
      [error_message] = dag_def.messages
      assert "undefined variable \"kaboomm\"" == error_message.message

      assert "cannot compile module CompiledErroredDag (errors have been logged)" ==
               dag_def.error.description
    end

    test "file has unknown task options", %{tmp_dir: dags_folder} do
      dag_definition = """
        defmodule MyInvalidTaskDag do
          use Gust.DSL, schedule: "* * * * *"

          task :bye, foo_bar: "hi" do
            # saying bye
          end

        end
      """

      dag_name = "my_name"
      file = "#{dags_folder}/#{dag_name}.ex"
      File.write!(file, dag_definition)

      {:ok, dag_def} = Adapter.parse_file(file)
      [error_message] = dag_def.messages

      assert "unknown keys [:foo_bar] in [foo_bar: \"hi\"], the allowed keys are: [:downstream, :store_result, :ctx, :skip_if, :map_over]" ==
               error_message.message

      assert "cannot compile module MyInvalidTaskDag (errors have been logged)" ==
               dag_def.error.description
    end

    test "file has unknown dag options", %{tmp_dir: dags_folder} do
      dag_definition = """
        defmodule MyValidDag do
          use Gust.DSL, foo_bar: "hi", schedule: "* * * * *"

        end
      """

      dag_name = "my_name"
      file = "#{dags_folder}/#{dag_name}.ex"
      File.write!(file, dag_definition)

      {:ok, dag_def} = Adapter.parse_file(file)

      assert %{
               message:
                 "unknown keys [:foo_bar] in [foo_bar: \"hi\", schedule: \"* * * * *\"], the allowed keys are: [:schedule, :on_finished_callback]"
             } = dag_def.error
    end
  end
end
