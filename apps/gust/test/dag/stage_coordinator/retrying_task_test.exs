defmodule DAG.StageCoordinator.RetryingTaskTest do
  use Gust.DataCase, async: false

  alias Gust.Flows
  import Gust.FlowsFixtures
  alias Gust.DAG.StageCoordinator.RetryingTask
  import Mox

  setup :verify_on_exit!

  setup do
    dag = dag_fixture()
    run = run_fixture(%{dag_id: dag.id})
    task = task_fixture(%{run_id: run.id, name: "hi"})
    %{run: run, task: task}
  end

  def create_upstreams(%{task: task, run: run}) do
    upstream_task1 = task_fixture(%{run_id: run.id, name: "john_mayer"})
    upstream_task2 = task_fixture(%{run_id: run.id, name: "jack_johnson"})
    tasks = %{task.name => %{upstream: [upstream_task1.name, upstream_task2.name]}}
    %{upstream_task1: upstream_task1, upstream_task2: upstream_task2, tasks: tasks}
  end

  describe "new/1" do
    test "fill struct with pending runs" do
      coord = RetryingTask.new([1, 2, 3])
      assert coord.running == MapSet.new([1, 2, 3])
    end
  end

  describe "process_task/2 when upstream is not present" do
    test "upstream does not exsists", %{task: task} do
      tasks = %{task.name => %{upstream: []}}
      assert RetryingTask.process_task(task, tasks) == :ok
    end
  end

  describe "process_task/2 when task staus is not :running" do
    test "already_processed return for failed status", %{task: task} do
      {:ok, task} = Flows.update_task_status(task, :failed)
      tasks = %{task.name => %{upstream: []}}
      assert RetryingTask.process_task(task, tasks) == :already_processed
    end

    test "already_processed return for succeeded status", %{task: task} do
      {:ok, task} = Flows.update_task_status(task, :succeeded)
      tasks = %{task.name => %{upstream: []}}
      assert RetryingTask.process_task(task, tasks) == :already_processed
    end

    test "already_processed return for upstream_failed status", %{task: task} do
      {:ok, task} = Flows.update_task_status(task, :upstream_failed)
      tasks = %{task.name => %{upstream: []}}
      assert RetryingTask.process_task(task, tasks) == :already_processed
    end

    test "already_processed return for skipped status", %{task: task} do
      {:ok, task} = Flows.update_task_status(task, :skipped)
      tasks = %{task.name => %{upstream: []}}
      assert RetryingTask.process_task(task, tasks) == :already_processed
    end

    test "already_processed return for retrying status", %{task: task} do
      {:ok, task} = Flows.update_task_status(task, :retrying)
      tasks = %{task.name => %{upstream: []}}
      assert RetryingTask.process_task(task, tasks) == :ok
    end
  end

  describe "process_task/2 when upstream is present" do
    setup [:create_upstreams]

    test "no upstream failed", %{
      task: task,
      upstream_task1: upstream_task1,
      upstream_task2: upstream_task2,
      tasks: tasks
    } do
      Flows.update_task_status(upstream_task1, :succeeded)
      Flows.update_task_status(upstream_task2, :succeeded)

      assert RetryingTask.process_task(task, tasks) == :ok
    end

    test "one upstream errored as failed", %{
      task: task,
      upstream_task1: upstream_task1,
      upstream_task2: upstream_task2,
      tasks: tasks
    } do
      Flows.update_task_status(upstream_task1, :succeeded)
      Flows.update_task_status(upstream_task2, :failed)

      assert RetryingTask.process_task(task, tasks) == :upstream_failed
    end

    test "one upstream skipped", %{
      task: task,
      upstream_task1: upstream_task1,
      upstream_task2: upstream_task2,
      tasks: tasks
    } do
      Flows.update_task_status(upstream_task1, :succeeded)
      Flows.update_task_status(upstream_task2, :skipped)

      assert RetryingTask.process_task(task, tasks) == :skipped
    end

    test "failed upstream takes precedence over skipped upstream", %{
      task: task,
      upstream_task1: upstream_task1,
      upstream_task2: upstream_task2,
      tasks: tasks
    } do
      Flows.update_task_status(upstream_task1, :failed)
      Flows.update_task_status(upstream_task2, :skipped)

      assert RetryingTask.process_task(task, tasks) == :upstream_failed
    end

    test "one upstream erroes as upstream_failed", %{
      task: task,
      upstream_task1: upstream_task1,
      upstream_task2: upstream_task2,
      tasks: tasks
    } do
      Flows.update_task_status(upstream_task1, :succeeded)
      Flows.update_task_status(upstream_task2, :upstream_failed)

      assert RetryingTask.process_task(task, tasks) == :upstream_failed
    end

    test "returns expand task when task has map_over option and upstream results", %{run: run} do
      source_task = task_fixture(%{run_id: run.id, name: "say_by", status: :succeeded})
      task = task_fixture(%{run_id: run.id, name: "insert_models"})
      results = [%{"model" => "a"}, %{"model" => "b"}]

      tasks = %{
        task.name => %{
          upstream: [source_task.name],
          map_over: :say_by
        }
      }

      Gust.DAGTaskExpanderMock
      |> expect(:get_params, fn "say_by", run_id, nil when run_id == run.id ->
        {:expand_task, results}
      end)

      assert RetryingTask.process_task(task, tasks) == {:expand_task, results}
    end
  end

  describe "put_running/2" do
    test "adds task_ids to running and marks them :running", %{run: run, task: task} do
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})
      sec_task_id = sec_task.id

      coord0 = %{
        running: MapSet.new([sec_task.id]),
        retrying: %{sec_task.id => 2}
      }

      new_running = MapSet.new([sec_task.id, task.id])

      assert %{
               running: ^new_running,
               retrying: %{^sec_task_id => 2}
             } =
               RetryingTask.put_running(coord0, task.id)
    end
  end

  describe "update_restart_timer/3" do
    test "adds restart_timer ref to the retrying task", %{task: task} do
      ref = make_ref()
      task_id = task.id
      coord0 = %{retrying: %{task.id => %{attempts: 2}}}

      assert %{retrying: %{^task_id => %{restart_timer: ^ref, attempts: 2}}} =
               RetryingTask.update_restart_timer(coord0, task, ref)
    end
  end

  describe "apply_task_result/3 for non-normal reason" do
    test "max retry has been reached without other retrying tasks", %{task: task} do
      coord = %RetryingTask{
        running: MapSet.new([task.id]),
        retrying: %{task.id => %{attempt: 3}}
      }

      result = RetryingTask.apply_task_result(coord, task, :error)

      assert {:finished, %RetryingTask{running: %MapSet{}, retrying: %{}}} =
               result
    end

    test "max retry has been reached with other retrying tasks", %{run: run, task: task} do
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})
      sec_task_id = sec_task.id

      coord = %RetryingTask{
        running: MapSet.new([task.id]),
        retrying: %{task.id => %{attempt: 3}, sec_task_id => %{attempt: 2}}
      }

      result = RetryingTask.apply_task_result(coord, task, :error)

      assert {:continue,
              %RetryingTask{running: %MapSet{}, retrying: %{^sec_task_id => %{attempt: 2}}}} =
               result
    end

    test "max retry has not been reached", %{run: run, task: task} do
      task_id = task.id
      Gust.PubSub.subscribe_run(run.id)
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})
      sec_task_id = sec_task.id

      coord = %RetryingTask{
        running: MapSet.new([task.id]),
        retrying: %{sec_task.id => %{attempt: 2}}
      }

      delay = 1818
      attempt = task.attempt

      Gust.DAGTaskDelayerMock |> expect(:calc_delay, fn ^attempt -> delay end)

      result = RetryingTask.apply_task_result(coord, task, :error)
      updated_task = Flows.get_task!(task.id)

      assert {:reschedule,
              %RetryingTask{
                running: %MapSet{},
                retrying: %{^task_id => %{attempt: 1}, ^sec_task_id => %{attempt: 2}}
              }, ^updated_task, ^delay} = result
    end

    test "task retrying one more time increments counter", %{run: run, task: task} do
      task_id = task.id
      Gust.PubSub.subscribe_run(run.id)
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})
      sec_task_id = sec_task.id

      coord = %RetryingTask{
        running: MapSet.new([task.id]),
        retrying: %{task.id => %{attempt: 1}, sec_task_id => %{attempt: 2}}
      }

      delay = 1818
      attempt = task.attempt
      Gust.DAGTaskDelayerMock |> expect(:calc_delay, fn ^attempt -> delay end)

      result = RetryingTask.apply_task_result(coord, task, :error)
      updated_task = Flows.get_task!(task.id)

      assert {:reschedule,
              %RetryingTask{
                running: %MapSet{},
                retrying: %{^task_id => %{attempt: 2}, ^sec_task_id => %{attempt: 2}}
              }, ^updated_task, ^delay} = result
    end
  end

  describe "apply_task_result/3 when already processed" do
    test "continue if tasks area running", %{task: task} do
      coord = %RetryingTask{running: MapSet.new([task.id]), retrying: %{}}

      assert {:finished, %RetryingTask{running: %MapSet{}, retrying: %{}}} ==
               RetryingTask.apply_task_result(coord, task, :already_processed)
    end

    test "tasks are left in running", %{run: run, task: task} do
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})

      coord = %RetryingTask{
        running: MapSet.new([task.id, sec_task.id]),
        retrying: %{}
      }

      remaining = MapSet.new([sec_task.id])

      assert {:continue, %RetryingTask{running: ^remaining, retrying: %{}}} =
               RetryingTask.apply_task_result(coord, task, :already_processed)
    end
  end

  describe "apply_task_result/3 when cancelled" do
    test "continue if tasks area running", %{task: task} do
      coord = %RetryingTask{running: MapSet.new([task.id]), retrying: %{}}

      assert {:finished, %RetryingTask{running: %MapSet{}, retrying: %{}}} ==
               RetryingTask.apply_task_result(coord, task, :cancelled)
    end
  end

  describe "apply_task_result/3 when upstream failed" do
    test "continue if tasks area running", %{task: task} do
      coord = %RetryingTask{running: MapSet.new([task.id]), retrying: %{}}

      assert {:finished, %RetryingTask{running: %MapSet{}, retrying: %{}}} ==
               RetryingTask.apply_task_result(coord, task, :upstream_failed)
    end

    test "tasks are left in running", %{run: run, task: task} do
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})

      coord = %RetryingTask{
        running: MapSet.new([task.id, sec_task.id]),
        retrying: %{}
      }

      remaining = MapSet.new([sec_task.id])

      assert {:continue, %RetryingTask{running: ^remaining, retrying: %{}}} =
               RetryingTask.apply_task_result(coord, task, :ok)
    end
  end

  describe "apply_task_result/3 when skipped" do
    test "finishes when no tasks are left running", %{task: task} do
      coord = %RetryingTask{running: MapSet.new([task.id]), retrying: %{}}

      assert {:finished, %RetryingTask{running: %MapSet{}, retrying: %{}}} ==
               RetryingTask.apply_task_result(coord, task, :skipped)
    end

    test "continues when other tasks are still running", %{run: run, task: task} do
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})

      coord = %RetryingTask{
        running: MapSet.new([task.id, sec_task.id]),
        retrying: %{task.id => %{attempt: 1}}
      }

      remaining = MapSet.new([sec_task.id])

      assert {:continue, %RetryingTask{running: ^remaining, retrying: %{}}} =
               RetryingTask.apply_task_result(coord, task, :skipped)
    end
  end

  describe "apply_task_result/3 for normal reason" do
    test "no tasks left in running and retrying was sucessful", %{task: task} do
      coord = %RetryingTask{running: MapSet.new([task.id]), retrying: %{task.id => 1}}

      assert RetryingTask.apply_task_result(coord, task, :ok) ==
               {:finished, %RetryingTask{running: %MapSet{}, retrying: %{}}}
    end

    test "no tasks left in running", %{task: task} do
      coord = %RetryingTask{running: MapSet.new([task.id]), retrying: %{}}

      assert {:finished, %RetryingTask{running: %MapSet{}, retrying: %{}}} ==
               RetryingTask.apply_task_result(coord, task, :ok)
    end

    test "tasks are left in running", %{run: run, task: task} do
      sec_task = task_fixture(%{run_id: run.id, name: "second_task"})

      coord = %RetryingTask{
        running: MapSet.new([task.id, sec_task.id]),
        retrying: %{}
      }

      remaining = MapSet.new([sec_task.id])

      assert {:continue, %RetryingTask{running: ^remaining, retrying: %{}}} =
               RetryingTask.apply_task_result(coord, task, :ok)
    end
  end
end
