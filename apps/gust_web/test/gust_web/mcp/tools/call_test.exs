defmodule GustWeb.MCP.Tools.CallTest do
  alias Gust.Flows
  use Gust.DataCase, async: true

  import Mox
  import Gust.FlowsFixtures

  alias Gust.DAG.Definition
  alias GustWeb.MCP.Content
  alias GustWeb.MCP.Tool
  alias GustWeb.MCP.Tools.Call
  alias GustWeb.MCP.Tools.List

  setup :verify_on_exit!
  setup :set_mox_from_context

  setup do
    dag_def = %Definition{
      name: "definition_dag",
      adapter: :elixir,
      options: [schedule: "@hourly"],
      tasks: %{
        "extract_data" => %{downstream: MapSet.new(["publish_data"]), upstream: MapSet.new()},
        "publish_data" => %{downstream: MapSet.new(), upstream: MapSet.new(["extract_data"])}
      },
      stages: [["extract_data"], ["publish_data"]],
      error: %{},
      mod: Gust.Example.DefinitionDag,
      file_path: "/tmp/dags/definition_dag.ex"
    }

    dag = dag_fixture(%{name: "definition_dag"})

    %{dag_def: dag_def, dag: dag}
  end

  setup %{dag_def: dag_def, dag: dag} do
    run = run_fixture(%{dag_id: dag.id})
    task = task_fixture(%{run_id: run.id, name: "transform_data", status: :failed})

    %{run: run, task: task, dag_def: dag_def, dag: dag}
  end

  setup :mock_load_definition

  defp mock_load_definition(%{mock_load_definition: true, dag: dag, dag_def: dag_def}) do
    dag_id = dag.id

    GustWeb.DAGLoaderMock
    |> expect(:get_definition, fn ^dag_id ->
      {:ok, dag_def}
    end)

    :ok
  end

  defp mock_load_definition(_other_context), do: :ok

  test "handle/2 returns text content for loaded DAG definitions and ignores non-ok entries", %{
    dag_def: dag_def
  } do
    dag = dag_fixture(%{name: "daily_stock_decider"})

    GustWeb.DAGLoaderMock
    |> expect(:get_definitions, fn ->
      %{
        dag.id => {:ok, dag_def},
        99 => {:error, :parse_failed}
      }
    end)

    assert {false, [%Content{} = content]} =
             Call.handle(%Tool{name: :list_dags}, %{"unused" => true})

    assert content.text == dag_definition_text(dag.id, dag_def, true)
  end

  test "handle/2 returns an empty content list when no DAG definitions are available" do
    GustWeb.DAGLoaderMock
    |> expect(:get_definitions, fn -> %{} end)

    assert {false, []} = Call.handle(%Tool{name: :list_dags}, %{})
  end

  test "handle/2 returns text content for secrets" do
    secret_1 =
      secret_fixture(%{
        name: "FIRST_SECRET",
        value: "alpha",
        value_type: :string
      })

    secret_2 =
      secret_fixture(%{
        name: "SECOND_SECRET",
        value: ~s({"enabled":true}),
        value_type: :json
      })

    assert {false, contents} = Call.handle(%Tool{name: :list_secrets}, %{})

    assert text_list(contents) |> Enum.sort() == [
             "Name: FIRST_SECRET; ID: #{secret_1.id}; Type: string",
             "Name: SECOND_SECRET; ID: #{secret_2.id}; Type: json"
           ]
  end

  test "handle/2 returns paginated runs for the requested dag ordered by newest first" do
    dag = dag_fixture(%{name: "sample_dag"})

    older_run =
      run_fixture(%{
        dag_id: dag.id,
        status: :failed,
        inserted_at: ~U[2026-01-01 00:00:00Z]
      })

    middle_run =
      run_fixture(%{
        dag_id: dag.id,
        status: :running,
        inserted_at: ~U[2026-01-02 00:00:00Z]
      })

    newest_run =
      run_fixture(%{
        dag_id: dag.id,
        status: :succeeded,
        inserted_at: ~U[2026-01-03 00:00:00Z]
      })

    assert {false, contents} =
             Call.handle(%Tool{name: :query_dag_run}, %{
               "dag_name" => dag.name,
               "limit" => 2,
               "offset" => 1
             })

    assert text_list(contents) == [
             "ID: #{middle_run.id}; Inserted at: #{middle_run.inserted_at}; " <>
               "Updated at: #{middle_run.updated_at}; Status: running",
             "ID: #{older_run.id}; Inserted at: #{older_run.inserted_at}; " <>
               "Updated at: #{older_run.updated_at}; Status: failed"
           ]

    refute run_text(newest_run, :succeeded) in text_list(contents)
  end

  test "handle/2 returns a not found error when query_dag_run receives an unknown dag_name" do
    dag_name = "missing_dag"

    assert {true, contents} =
             Call.handle(%Tool{name: :query_dag_run}, %{
               "dag_name" => dag_name,
               "limit" => 10,
               "offset" => 0
             })

    assert text_list(contents) == [dag_not_found_text(dag_name)]
  end

  @tag :mock_load_definition
  test "handle/2 returns dag definition details for the requested dag id", %{
    dag: dag,
    dag_def: dag_def
  } do
    assert {false, [%Content{} = content]} =
             Call.handle(%Tool{name: :get_dag_def}, %{"dag_id" => dag.id})

    assert content.text == dag_definition_text(dag.id, dag_def, dag.enabled)
  end

  test "handle/2 returns dag definition details when the loaded definition includes an error", %{
    dag: dag
  } do
    dag_id = dag.id
    error = %{line: 9, message: "unexpected token"}

    GustWeb.DAGLoaderMock
    |> expect(:get_definition, fn ^dag_id ->
      {:error, error}
    end)

    assert {false, [%Content{} = content]} =
             Call.handle(%Tool{name: :get_dag_def}, %{"dag_id" => dag.id})

    assert content.text == "DAG with ID #{dag_id} cannot be parsed, error: #{inspect(error)}"
  end

  @tag :mock_load_definition
  test "handle/2 returns dag definition details for the requested dag name", %{
    dag_def: dag_def,
    dag: dag
  } do
    assert {false, [%Content{} = content]} =
             Call.handle(%Tool{name: :get_dag_def}, %{"dag_name" => dag.name})

    assert content.text == dag_definition_text(dag.id, dag_def, dag.enabled)
  end

  @tag :mock_load_definition
  test "handle/2 enables a dag, dispatches pending runs, and returns the updated definition", %{
    dag_def: dag_def,
    dag: dag
  } do
    {:ok, dag} = Gust.Flows.toggle_enabled(dag)

    GustWeb.DAGRunTriggerMock
    |> expect(:dispatch_all_runs, fn dag_id ->
      assert dag_id == dag.id
      nil
    end)

    assert {false, [%Content{} = content]} =
             Call.handle(%Tool{name: :toggle_enabled_dag}, %{"dag_id" => dag.id})

    assert content.text == dag_definition_text(dag.id, dag_def, true)
    assert Gust.Flows.get_dag!(dag.id).enabled
  end

  @tag :mock_load_definition
  test "handle/2 disables a dag without dispatching runs and returns the updated definition", %{
    dag_def: dag_def,
    dag: dag
  } do
    assert {false, [%Content{} = content]} =
             Call.handle(%Tool{name: :toggle_enabled_dag}, %{"dag_id" => dag.id})

    assert content.text == dag_definition_text(dag.id, dag_def, false)
    refute Gust.Flows.get_dag!(dag.id).enabled
  end

  test "handle/2 returns a not found error when get_dag_def receives an unknown dag_name" do
    dag_name = "missing_dag"

    assert {true, contents} =
             Call.handle(%Tool{name: :get_dag_def}, %{"dag_name" => dag_name})

    assert text_list(contents) == [dag_not_found_text(dag_name)]
  end

  test "handle/2 returns task details for the requested run" do
    dag = dag_fixture(%{name: "tasks_dag"})
    run = run_fixture(%{dag_id: dag.id})

    task_1 =
      task_fixture(%{
        run_id: run.id,
        name: "extract_prices",
        status: :failed,
        error: %{"reason" => "boom"},
        result: %{}
      })

    task_2 =
      task_fixture(%{
        run_id: run.id,
        name: "publish_report",
        status: :succeeded,
        error: %{},
        result: %{"ok" => true}
      })

    assert {false, contents} = Call.handle(%Tool{name: :get_tasks_on_run}, %{"run_id" => run.id})

    assert text_list(contents) |> Enum.sort() == [
             task_text(task_1),
             task_text(task_2)
           ]
  end

  test "handle/2 returns logs for the requested task", %{task: task} do
    log_1 =
      log_fixture(%{
        task_id: task.id,
        content: "Started fetching data",
        level: "info",
        attempt: 1
      })

    log_2 =
      log_fixture(%{
        task_id: task.id,
        content: "Retry scheduled",
        level: "warn",
        attempt: 1
      })

    assert {false, contents} =
             Call.handle(%Tool{name: :get_logs_on_task}, %{"task_id" => task.id})

    assert text_list(contents) |> Enum.sort() == [
             log_text(log_1),
             log_text(log_2)
           ]
  end

  test "handle/2 delegates restart_run to the configured trigger", %{run: run} do
    {:ok, run} = Flows.update_run_status(run, :failed)

    GustWeb.DAGRunTriggerMock
    |> expect(:reset_run, fn %Gust.Flows.Run{id: run_id} = fetched_run ->
      assert run_id == run.id
      fetched_run
    end)

    assert {false, contents} = Call.handle(%Tool{name: :restart_run}, %{"run_id" => run.id})
    assert text_list(contents) == ["Run: #{run.id} was restarted"]
  end

  @tag :mock_load_definition
  test "handle/2 delegates restart_task with the dag task graph and task", %{
    dag_def: %Definition{tasks: tasks},
    task: %Gust.Flows.Task{name: t_name} = task
  } do
    GustWeb.DAGRunTriggerMock
    |> expect(:reset_task, fn ^tasks, ^task, nil ->
      []
    end)

    assert {false, contents} = Call.handle(%Tool{name: :restart_task}, %{"task_id" => task.id})
    assert text_list(contents) == ["Task: #{t_name} was restarted"]
  end

  @tag :mock_load_definition
  test "handle/2 cancels a running task via the terminator", %{
    task: task
  } do
    {:ok, task} = Flows.update_task_status(task, :running)

    GustWeb.DAGTerminatorMock
    |> expect(:kill_task, fn ^task, :cancelled, _runtime ->
      nil
    end)

    assert {false, contents} = Call.handle(%Tool{name: :cancel_task}, %{"task_id" => task.id})
    assert text_list(contents) == ["Task: #{task.name} was cancelled"]
  end

  @tag :mock_load_definition
  test "handle/2 cancels a retrying task timer via the terminator", %{
    task: task
  } do
    {:ok, task} = Flows.update_task_status(task, :retrying)

    GustWeb.DAGTerminatorMock
    |> expect(:cancel_timer, fn ^task, :cancelled ->
      nil
    end)

    assert {false, contents} = Call.handle(%Tool{name: :cancel_task}, %{"task_id" => task.id})
    assert text_list(contents) == ["Task: #{task.name} retrying cancelled"]
  end

  @tag :mock_load_definition
  test "handle/2 returns a message when cancel_task receives a task in an unsupported status", %{
    task: task
  } do
    assert {false, contents} = Call.handle(%Tool{name: :cancel_task}, %{"task_id" => task.id})

    assert text_list(contents) == [
             "Task: #{task.name} cannot be cancelled from status :failed. Only :running and :retrying tasks can be cancelled."
           ]
  end

  test "handle/2 creates and dispatches a run for the requested dag" do
    dag = dag_fixture(%{name: "triggerable_dag"})

    GustWeb.DAGRunTriggerMock
    |> expect(:dispatch_run, fn %Gust.Flows.Run{} = run ->
      assert run.dag_id == dag.id
      assert Ecto.assoc_loaded?(run.tasks)
      run
    end)

    assert {false, contents} =
             Call.handle(%Tool{name: :trigger_dag_run}, %{"dag_name" => dag.name})

    [message] = text_list(contents)
    assert String.starts_with?(message, "Run ")
    assert message =~ " triggered"
  end

  test "handle/2 creates and dispatches a run with params for the requested dag" do
    dag = dag_fixture(%{name: "triggerable_dag_with_params"})
    dag_id = dag.id
    params = %{"name" => "foo", "attempt" => 2}

    GustWeb.DAGRunTriggerMock
    |> expect(:dispatch_run, fn %Gust.Flows.Run{params: ^params, dag_id: ^dag_id} = run ->
      assert Ecto.assoc_loaded?(run.tasks)
      run
    end)

    assert {false, contents} =
             Call.handle(%Tool{name: :trigger_dag_run}, %{
               "dag_name" => dag.name,
               "params" => params
             })

    [message] = text_list(contents)
    assert String.starts_with?(message, "Run ")
    assert message =~ " triggered"
  end

  test "handle/2 creates and dispatches a run for the requested dag id" do
    dag = dag_fixture(%{name: "triggerable_dag_by_id"})

    GustWeb.DAGRunTriggerMock
    |> expect(:dispatch_run, fn %Gust.Flows.Run{} = run ->
      assert run.dag_id == dag.id
      assert Ecto.assoc_loaded?(run.tasks)
      run
    end)

    assert {false, contents} =
             Call.handle(%Tool{name: :trigger_dag_run}, %{"dag_id" => dag.id})

    [message] = text_list(contents)
    assert String.starts_with?(message, "Run ")
    assert message =~ " triggered"
  end

  test "handle/2 returns a not found error when trigger_dag_run receives an unknown dag_name" do
    dag_name = "missing_dag"

    assert {true, contents} =
             Call.handle(%Tool{name: :trigger_dag_run}, %{"dag_name" => dag_name})

    assert text_list(contents) == [dag_not_found_text(dag_name)]
  end

  test "handle/2 returns a fallback error describing supported properties" do
    tool = List.find("query_dag_run")

    GustWeb.MCPToolsMock
    |> expect(:find, fn "query_dag_run" -> tool end)

    assert {true, contents} =
             Call.handle(%Tool{name: :query_dag_run}, %{"unexpected" => "value"})

    assert text_list(contents) == [
             "Tool query_dag_run supports the following properties: " <>
               "dag_name: The DAG name in lowercase. Use underscores for compound names, e.g. my_dag, " <>
               "limit: Maximum number of runs to return. Defaults to 10 if not specified., " <>
               "offset: Number of runs to skip for pagination. Defaults to 0 if not specified."
           ]
  end

  test "handle/2 falls back to the original tool when the tool registry has no match" do
    GustWeb.MCPToolsMock
    |> expect(:find, fn "unknown_tool" -> nil end)

    assert {true, contents} =
             Call.handle(%Tool{name: :unknown_tool, props: []}, %{"unexpected" => "value"})

    assert text_list(contents) == ["Tool unknown_tool supports no properties."]
  end

  defp text_list(contents), do: Enum.map(contents, & &1.text)

  defp dag_definition_text(dag_id, dag_def, enabled) do
    """
    Name: #{dag_def.name}
    ID: #{dag_id}
    Enabled: #{enabled}
    File Path: #{dag_def.file_path}
    Options: #{inspect(dag_def.options)}
    Stages: #{inspect(dag_def.stages)}
    Module: #{dag_def.mod}
    Adapter: #{dag_def.adapter}
    Tasks: #{inspect(dag_def.tasks)}
    Error: #{inspect(dag_def.error)}
    Warnings: #{inspect(dag_def.messages)}
    """
  end

  defp run_text(run, status) do
    "ID: #{run.id}; Inserted at: #{run.inserted_at}; Updated at: #{run.updated_at}; Status: #{status}"
  end

  defp task_text(task) do
    "ID: #{task.id}; Name: #{task.name}, Status: #{task.status}; Error: #{inspect(task.error)}, Result: #{inspect(task.result)}"
  end

  defp log_text(log) do
    "ID: #{log.id}; level: #{log.level}, inserted_at: #{inspect(log.inserted_at)}; Content: #{log.content}"
  end

  defp dag_not_found_text(dag_name) do
    "DAG with name #{dag_name} does not exist. Use list_dags to find available DAG names"
  end
end
